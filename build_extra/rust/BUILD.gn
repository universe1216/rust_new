# Copyright 2018 the Deno authors. All rights reserved. MIT license.

# Dependencies between third party crates is mapped out here manually.  This is
# not so difficult and having it be tedious to add dependencies might help us
# avoid dependency hell later on. Always try to minimize dependencies.
# Versioning for third party rust crates is controlled in //Cargo.toml
# Use //tools/sync_third_party.py instead of running "cargo install".

import("rust.gni")

crates = "//third_party/rust_crates"
registry_github = "$crates/registry/src/github.com-1ecc6299db9ec823/"

rust_component("libc") {
  source_root = "$registry_github/libc-0.2.42/src/lib.rs"
  features = [ "use_std" ]
}

rust_component("url") {
  source_root = "$registry_github/url-1.7.1/src/lib.rs"
  extern = [
    ":matches",
    ":idna",
    ":percent_encoding",
  ]
}

rust_component("percent_encoding") {
  source_root = "$registry_github/percent-encoding-1.0.1/lib.rs"
  args = [
    # TODO: Suppress some warnings at this moment
    # This should be removed when it's fixed in servo/rust-url repository
    # https://github.com/servo/rust-url/issues/455
    "-Aunused-imports",
    "-Adeprecated",
  ]
}

rust_component("matches") {
  source_root = "$registry_github/matches-0.1.6/lib.rs"
}

rust_component("idna") {
  source_root = "$registry_github/idna-0.1.5/src/lib.rs"
  extern = [
    ":matches",
    ":unicode_bidi",
    ":unicode_normalization",
  ]
}

rust_component("unicode_bidi") {
  source_root = "$registry_github/unicode-bidi-0.3.4/src/lib.rs"
  extern = [ ":matches" ]
}

rust_component("unicode_normalization") {
  source_root = "$registry_github/unicode-normalization-0.1.7/src/lib.rs"
}

rust_component("log") {
  source_root = "$registry_github/log-0.4.3/src/lib.rs"
  extern = [ ":cfg_if" ]
}

rust_component("cfg_if") {
  source_root = "$registry_github/cfg-if-0.1.4/src/lib.rs"
}

rust_component("sha1") {
  source_root = "$registry_github/sha1-0.6.0/src/lib.rs"
}

rust_component("tempfile") {
  source_root = "$registry_github/tempfile-3.0.3/src/lib.rs"
  extern = [
    ":libc",
    ":rand",
    ":remove_dir_all",
    ":winapi",
  ]
}

rust_component("rand") {
  source_root = "$registry_github/rand-0.5.4/src/lib.rs"
  extern = [
    ":libc",
    ":rand_core",
  ]
  if (is_win) {
    extern += [ ":winapi" ]
  }
  features = [
    "std",
    "alloc",
  ]
  if (is_mac) {
    libs = [ "Security.framework" ]
  }
}

rust_component("rand_core") {
  source_root = "$registry_github/rand_core-0.2.1/src/lib.rs"
}

rust_component("remove_dir_all") {
  source_root = "$registry_github/remove_dir_all-0.5.1/src/lib.rs"
  extern = []
  if (is_win) {
    extern += [ ":winapi" ]
  }
}

rust_component("winapi") {
  source_root = "$registry_github/winapi-0.3.5/src/lib.rs"
  features = [
    "basetsd",
    "cfg",
    "cfgmgr32",
    "errhandlingapi",
    "excpt",
    "fileapi",
    "guiddef",
    "handleapi",
    "ktmtypes",
    "libloaderapi",
    "lsalookup",
    "minwinbase",
    "minwindef",
    "ntdef",
    "ntsecapi",
    "ntstatus",
    "processthreadsapi",
    "profileapi",
    "sspi",
    "std",
    "subauth",
    "vadefs",
    "vcruntime",
    "winbase",
    "wincred",
    "windef",
    "winerror",
    "winnt",
    "winreg",
  ]
}
